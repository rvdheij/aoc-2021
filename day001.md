To compare numbers in consecutive lines, create lines with pairs of numbers from consecutive lines. Like this:
```
pipe literal 1 2 3 4 5 | split | dup | drop | drop last | join , , | cons
1 2
2 3
3 4
4 5
Ready; T=0.01/0.01 10:15:25
```
Since the number in our data are not all the same length, we need to right-justify them to do a string comparison.
```
PIPE < input001 txt | spec w1 1.6 r | dup | drop | drop last | join , , | pick 1.6 << 8.6 | count lines | cons
1400
Ready; T=0.02/0.02 10:20:26
```
### Subroutine
It is convenient to put that pair-wise comparison in a subroutine pipeine to build pairs of numbers and compare them.
```
/* COMPPAIR REXX      Compare pairs of consecutive numbers           */
/*                    Author: Rob van der Heij, 1 Dec 2021           */

'callpipe (name COMPPAIR.REXX:4)',
   '| *: ',
   '| dup ',
   '| drop ',
   '| drop last ',
   '| spec w1 1.10 r ',
   '| join ',
   '| pick 1.10 << 11.10 ',
   '| *: '

return rc * ( rc <> 12 )```
The subroutine can be used to simplify the original pipeline.
```
pipe < input001 txt | comppair | count lines | cons
1400
Ready; T=0.01/0.02 11:37:30
```
## Part 2
To form runs of 3 numbers, we can repeat the process to pair consecutive records, like this:
```
pipe literal 1 2 3 4 5 6 7 8 9 | split | dup | drop | drop last | join , , | dup | drop | drop last | join , , | cons
1 2 2 3
2 3 3 4
3 4 4 5
4 5 5 6
5 6 6 7
6 7 7 8
7 8 8 9
Ready; T=0.01/0.01 11:45:52
```
The only problem is that we have the middle number twice, so instead of the ```join``` we can use ```spec``` to combine the two.
```
To build the series of 3 numbers and sum them, we create another subroutine.
```
/* SUMTHREE REXX      Compute sum of 3 consecutive numbers           */
/*                    Author: Rob van der Heij, 1 Dec 2021           */

'callpipe (name SUMTHREE.REXX:4)',
   '| *: ',
   '| dup ',                          /* Make pairs                  */
   '| drop ',                         /* Align by dropping first     */
   '| drop last ',                    /* .. and last                 */
   '| join , , ',
   '| dup ',                          /* Make pairs of pairs         */
   '| drop ',
   '| drop last ',
   '| spec w1 1 read w1-* nw ',       /* Remove middle number        */
   '| spec a:w1 - b:w2 - c:w3 - print a+b+c 1.6 r ',     /* Add them */
   '| *: ',

error: return rc * ( rc <> 12 )
```
This subroutine can be used to compute the requested 3-number sum, and then the old ```comppair``` subroutine will count the sums that increase.
```
pipe < input001 txt | sumthree | comppair | count lines | cons
1429
Ready; T=0.04/0.05 11:54:20
```
