The input can be transformed into X,Y coordinate increments. So "forward 10" is transformed to "10 0" and "up 5" will be "0 -5". 
When we have that sequence, we just need sum both X and Y coordinates.

To produce the coordinate records, the ```deal streamid``` does an elegant trick by dealing the input records over its output streams based on data in the reocord.
Since CMS Pipelines stream identifiers are up to 4 characters, we can deal on the first 4 characters of the word
(actually the first character would be enough, but this makes it more readable). We code this as a subroutine that can be tested in isolation.
```
/* MAKECOOR REXX      Transform the up/down/forward n into dX dY     */
/*                    Author: Rob van der Heij, 2 Dec 2021           */

'callpipe (end \ name MAKECOOR.REXX:4)',
   '\ *: ',
   '| d: deal streamid substr 1.4 of w1 ',        /* Deal on keyword */
   '\ d.up: ',
   '| spec ,0 -, 1 w2 n ',            /* Up is as  "0 -n"            */
   '| i: faninany ',
   '| *: ',
   '\ d.down: ',
   '| spec ,0 , 1 w2 n ',             /* Down is "0 n"               */
   '| i: ',
   '\ d.forw: ',
   '| spec w2 1 ,0, nw',              /* Forward is "n 0"            */
   '| i: '

error: return rc * ( rc <> 12 )
```
We could argue that a cascade of ```pick``` stages could do the same with even less code, but this is a nice illustration of ```deal``` and easier
to adapt when new words are added. Testing the subroutine with the first 10 records shows this:
```
PIPE < input002 txt | makecoor | take 10 | cons
4 0
0 9
2 0
2 0
0 7
0 -2
0 9
0 -8
0 7
0 9
Ready; T=0.01/0.01 13:19:42
```
We can now add the two columns with a ```SPEC``` stage. Be aware to escape the # characters if you do this on the command line.
```
PIPE < input002 txt | makecoor | spec printonly eof h:w1 - v:w2 - set #0+=h;#1+=v eof print #0 1 print #1 nw | cons
       2065         917
Ready; T=0.04/0.04 13:21:08
```
Since the challenge was to multiply the two numbers, we can have ```SPEC``` do that as well.
```
PIPE < input002 txt | makecoor | spec printonly eof h:w1 - v:w2 - set #0+=h;#1+=v eof print #0*#1 1 | cons
    1893605
Ready; T=0.02/0.02 13:23:22
```
