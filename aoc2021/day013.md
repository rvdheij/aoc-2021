# Folding a matrx

The input consists of a series of (x,y) coordinates of points on
a grid, and instructions on how to "fold" the grid (such that
the folded sides overlap and points overlay in the folded grid).

The initial reaction is to start plotting the points in a grid.
An elegant approach is to use the variable output placement in a
SPEC stage to position a point in a column.Assume we have the
data shaped with just row and column in 10-byte fields.
```
   | spec y:1.10 1.10 x: 11.10 - ,*, (x+12) '
```
This takes the x-coordinate and positions the '*' at the right
column (compensate for the 10-byte row number). Intermediate
positions are filled with spaces as needed. We now have
still one line per point, but when the points are sorted on
row number, a ```unique 1.10 last``` can identify the last
point in a run. The classic trick to use is the ```overstr```
stage that emulates a 1403 printer, using the control character
in the first column to advance the paper by one line.
The result is a single record for each row with the points
in the right positions.

```
PIPE < small013 txt | locate /,/ | spec fs , f2 1.10 r f1 n.10 r | day13f | cons
         0    *  *  *
         1     *
         3 *
         4    *    * *
        10  *    * **
        11     *
        12       *   *
        13 *
        14 * *
Ready; T=0.01/0.01 13:51:39
```

## Folding the Grid

When we have the points in a grid like shown above, we could fold along
a row by splitting the records in two groups and overlay each pair of
records. But before we can do that, we need to create records for the
rows without any points. The WHILE clause in SPEC can do that nicely.
```
spec a: w1 -
   while #0<a do
     print #0 1.10 r write
   done
   set #0+=1 1-* 1
```
We use the #0 counter to keep track of the last line seen (starting at
zero) and use the ```while``` clause to generate the missing records.

To fold the grid along ```x=n``` we can ```chop``` the line at the
right column and reverse the chopped off segment, and then ```combine```
with the left-hand side.
```
     '| z: chop' pos+12 ,             /* Chop after fold column      */
     '| copy ',
     '| c: combine or ',              /* Overlay left and right side */
     '| *: ',
     '\ z: ',
     '| pad' pos+1 ,                  /* Ensure it is full width     */
     '| reverse ',
     '| spec 1-* 11 ',                /* Position beyond row number  */
     '| c: '
```
Because the lines are only as long as the last point on that row, we
need to ```pad``` the record before trying to reverse it. The rest of the
tweaking is to avoid the row number on the first positions of the record.

I ended up with a series of subroutines that are easy to test in isolation.
When we combine all the subroutines, the result is nice and doesn't take
too long.
```
pipe < input013 txt | day13h | cons
         0  **  ****   ** *  * *    *  *  **    **
         1 *  * *       * * *  *    *  * *  *    *
         2 *    ***     * **   *    *  * *       *
         3 *    *       * * *  *    *  * * **    *
         4 *  * *    *  * * *  *    *  * *  * *  *
         5  **  ****  **  *  * ****  **   ***  **
Ready; T=0.10/0.11 19:36:49
```

## Pipethink Solution

With some more Pipethink, we can recognize that the "fold" instructions
really are simple transformations that move a point on the X- or Y-axis.
It would be more attractive to build a pipeline to perform the
transformations, and simply feed the coordinates of each point through
them. Only at the end, we need to paint the remaining few points on a
grid.

To fold along the x=p position, all points with X-coordinate beyond p
will be mapped to a position that is (x-p) left of p. That means the
new X-coordinate is ```2*p-x```.
```
    '| spec a: 1.10  1 b: 11.10 11 ',
           'if b>'p 'then print '2*p'-b 11.10 r fi'
```
Folding along the y=p line is very similar, except that it changes the
Y-coordinate instead.

A simple subrouting can take the transformations through the secondary
input stream to build the pipeline, and then feed all the corrdinates
through.
```
'callpipe *.input.1: | substr w3 | stem fold.'    /* Transformations */
pipe = ''
do i = 1 to fold.0
  parse var fold.i dir '=' p .
  if dir = 'x' then
    comp = 'if b>'p 'then print '2*p'-b 11.10 r fi'
  else
    comp = 'if a>'p 'then print '2*p'-a  1.10 r fi'

  pipe = pipe '| spec a: 1.10  1 b: 11.10 11 ' comp
  'readto'
end
'callpipe (end \) *:' pipe '| *: '
```
This produces the list of coordinates that we can plot on a grid to
make it visible.Not only took this less logic, it also runs faster.
```
pipe < input013 txt | day13c | day13f | cons
         0  **  ****   ** *  * *    *  *  **    **
         1 *  * *       * * *  *    *  * *  *    *
         2 *    ***     * **   *    *  * *       *
         3 *    *       * * *  *    *  * * **    *
         4 *  * *    *  * * *  *    *  * *  * *  *
         5  **  ****  **  *  * ****  **   ***  **
Ready; T=0.07/0.08 20:05:07
```





