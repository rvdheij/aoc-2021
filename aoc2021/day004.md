## Part 1
We can break down the cells of the bingo board in
separate records, and tag each cell with the board
number, row number and column number.
```




```
This looks as shown (the scale is added for easy reading).
```
PIPE (END \) < small004 txt | drop | bingo | take 10 | scale | cons
....+....1....+....2....+....3....+....4
        22         1         1         1
        13         1         1         2
        17         1         1         3
        11         1         1         4
         0         1         1         5
         8         1         2         1
         2         1         2         2
        23         1         2         3
         4         1         2         4
        24         1         2         5
Ready; T=0.01/0.01 14:25:11
```
We can now store the cells with coordinates in the reference
table of a ```lookup``` stage. The ```allmasters``` option
will retrieve all the board cells that match a chosen number.
```
/* BINGOHIT REXX      Match the chosen numbers against board cells   */
/*                    Author: Rob van der Heij, 4 Dec 2021           */

'callpipe (end \ name BINGOHIT.REXX:4)',
   '\ *: ',
   '| t: take ',                      /* The numbers to score        */
   '| buffer ',
   '| split , ',
   '| l: lookup w1 allmasters',
   '| *: ',
   '\ t: ',
   '| bingo ',                     /* Tag with board, row and column */
   '| l: '

error: return rc * ( rc <> 12 )
```
This is shown below for the first 9 hits (numbers 7, 4, 9).
```
PIPE < small004 txt | bingohit | take 9 | cons
         7         1         3         5
         7         2         3         3
         7         3         5         5
         4         1         2         4
         4         2         4         5
         4         3         1         5
         9         1         3         2
         9         2         2         1
         9         3         2         4
```
To count the number of marked cells in a row on each of the
boards, we can score these lines on the 2nd and 3rd word (the
board and row number). We can do that with ```lookup``` in a very
elegant way, but the options used may need some clarification.
```
lookup autoadd before trackcount count keyonly 11.20 master.
```
The ```count``` is used to count the number of times a detail record
matched the master. The ```trackcount``` causes the matched master
record to be prefixed with the running count so far. The ```autoadd```
simply adds each new key automatically to the reference table; the
```before``` means it is added before searching the table, so basically
each new key is matched already the first time. Finally, the ```11.20```
input range specifies the key is the combination of board and row
number.
```
         1         1         3
         1         2         3
         1         3         5
         1         1         2
         1         2         4
         1         3         1
         2         1         3
         1         2         2
         1         3         2
         1         1         4
```
This shows that most have only one hit so far, except for board
1 row 3 that scored both twice so far. Since we need to find the
first number that scores a full row of cells marked, we wait
until we see a count of 5.

Since we also need to know the number that was matched, we need
a slightly more complicated pipeline to do it.
```
/* BINGOCNT REXX      Report matched cells with running count        */
/*                    Author: Rob van der Heij, 4 Dec 2021           */

'callpipe (end \ name BINGOCNT.REXX:7)',
   '\ *: ',
   '| bingohit ',                     /* Report matched bingo cells  */
   '| o: fanout ',
   '| chop 10 ',                      /* Keep just chosen number     */
   '| j: juxtapose ',                 /* .. as prefix                */
   '| *: ',
   '\ o: ',
   '| lookup autoadd before trackcount count keyonly 11.20 master ',
   '| j: '

error: return rc * ( rc <> 12 )
```
The output below shows the chosen number, the count by board/row, and
the board and row numbers.

```
PIPE (END \) < small004 txt | bingocnt | take 10 | cons
         7         1         1         3
         7         1         2         3
         7         1         3         5
         4         1         1         2
         4         1         2         4
         4         1         3         1
         9         2         1         3
         9         1         2         2
         9         1         3         2
         5         1         1         4
Ready; T=0.01/0.01 15:22:37
```
All we need to do is find the first line that has a count of 5,
as that is a full row in a board (this appears the number 24
that matched board 3 row 1 as 5th marked cell).
```
PIPE < small004 txt| bingocnt | pick w2 = ,5, | take | cons
        24         5         3         1
```
We can now use the same trick on the board/column pairs to find
the first number that makes a full column in a board. We extend
the BINGOCNT stage to keep track of both row- and column scores.
Some extra work is needed to get board and column number together
so that they can be used as the key.
```
/* BINGOCNT REXX      Score against both rows and columns            */
/*                    Author: Rob van der Heij, 4 Dec 2021           */

'callpipe (end \ name BINGOCNT.REXX:7)',
   '\ *: ',
   '| bingohit ',                     /* Report matched bingo cells  */
   '| o: fanout ',
   '| chop 10 ',                      /* Keep just chosen number     */
   '| j: juxtapose ',                 /* .. as prefix                */
   '| *: ',
   '\ o: ',
   '| spec 11.20 1 ',                 /* Board and row as key        */
   '| lookup autoadd before trackcount count 1.20 master ',
   '| j: ',
   '\ o: ',
   '| spec 11.10 1 31.10 n ',         /* Board and column as key     */
   '| lookup autoadd before trackcount count 1.20 master ',
   '| j: '

error: return rc * ( rc <> 12 )
```
When we look at the first few numbers that achieve a full row or
column, it looks like this.
```
PIPE (END \) < small004 txt|bingocnt | pick w2 = ,5, | take 4 | cons
        24         5         3         1
        16         5         1         3
        13         5         2         3
        12         5         1         2
Ready; T=0.01/0.02 20:49:31
```
Note that we can't tell whether it was the first row or column, but it
does tell us the first board to get a score was #3.

The next challenge is to tell which numbers on board 3 were not marked.
To determine which cells will still score in the winning board, we
can simply continue the game but add up the numbers once a board won.

For this we, need a latch that will let records pass once the board has
scored a full row or column. A ```lookup``` stage works like that when
we add the winners to the reference table.

Since each number matches twice on a board (for the row and column)
we use the ```unique``` stage to avoid counting both.

We then use another ```lookup``` to add up the numbers per board.
Using the ```increment``` option, the number is added to the total
instead of a simple count by 1. We can't combine this with the previous
```lookup``` stage because the duplicates are dropped in between.

In the process, we also add a sequence number to the winners, so it
is easy to pick the first and last winner (the 2nd ```lookup``` does
not report in order anymore).

```
/* BINGOADD REXX      Add remaining numbers for winners              */
/*                    Author: Rob van der Heij, 5 Dec 2021           */

'callpipe (end \ name BINGOADD.REXX:5)',
   '\ *: ',
   '| o: not fanout ',
   '| copy ',
   '| l: lookup w3 detail ',          /* Only look at winners        */
   '| unique (w1 w3) first ',         /* Avoid double per board      */
   '| spec 1.10 1 1-* 11 number n.10 r ', /* Use number as increment */
   '| k: lookup autoadd count increment w3 ', /* Sum remain by board */
   '\ l: ',
   '\ o: ',
   '| pick w2 == ,5, ',               /* Winning row or column       */
   '| l: ',                           /* .. add to winners list      */
   '\ k: ',                           /* No input, discard unmatched */
   '\ k: ',                           /* Reference with count        */
   '| *: '

error: return rc * ( rc <> 12 )
```
To find the first winning board, we ```sort``` and ```take``` now.
```
PIPE (END \) < input004 txt | bingocnt | bingoadd |sort 51.10 |take first | cons
       809        64         5        37         4         1
```
The solution of the puzzle is to multiply the first two numbers. You can
even do that with CMS Pipelines if you want:
```
  .. | spec a: w1 - b: w2 - print a*b 1 | cons
```
Since we compute the score for all boards, a ```take last``` will show the
solution for the second puzzle.
